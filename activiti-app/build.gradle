/*
 *  Copyright (C) 2005-2015 Alfresco Software Limited.
 *
 * This file is part of Alfresco Activiti Mobile for Android.
 *
 * Alfresco Activiti Mobile for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Alfresco Activiti Mobile for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Alfresco. If not, see <http://www.gnu.org/licenses/>.
 *
 */


apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId APPLICATION_ID

        minSdkVersion 19
        targetSdkVersion 22
        versionCode 10
        versionName VERSION_NAME

        manifestPlaceholders = [PROVIDER_AUTHORITY: PROVIDER_AUTHORITY]
        resValue "string", "account_name", ACCOUNT_ID
        resValue "string", "sync_adapter_app", SYNC_ADAPTER_APPS
        resValue "string", "sync_adapter_processModel", SYNC_ADAPTER_PROCESSDEF_MODEL
        resValue "string", "sync_adapter_group", SYNC_ADAPTER_GROUPS
        resValue "string", "sync_adapter_transfer", SYNC_ADAPTER_TRANSFER
        resValue "string", "sync_adapter_integration", SYNC_ADAPTER_INTEGRATION

        buildConfigField "String", "PROVIDER_AUTHORITY", "\"" + PROVIDER_AUTHORITY + "\""
        buildConfigField "String", "ACCOUNT_ID", "\"" + ACCOUNT_ID + "\""

        resValue "string", "bamboo_buildnumber", BUILDNUMBER

        //ALFRESCO Integration
        buildConfigField "String", "ALFRESCO_APPLICATION_ID", "\"" + ALFRESCO_APPLICATION_ID + "\""
        buildConfigField "String", "ALFRESCO_ACCOUNT_ID", "\"" + ALFRESCO_ACCOUNT_ID + "\""
        buildConfigField "String", "ALFRESCO_PROVIDER_AUTHORITY", "\"" + ALFRESCO_PROVIDER_AUTHORITY + "\""
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        custom {
            //Add here your custom sign configuration
        }

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }


    buildTypes {

        debug {
            // Important : In case of custom application
            // Don't forget to change all values to custom debug values
            applicationIdSuffix ".debug"

            manifestPlaceholders = [PROVIDER_AUTHORITY: PROVIDER_AUTHORITY_DEBUG]
            resValue "string", "account_name", ACCOUNT_ID_DEBUG
            resValue "string", "sync_adapter_app", SYNC_ADAPTER_APPS_DEBUG
            resValue "string", "sync_adapter_processModel", SYNC_ADAPTER_PROCESSDEF_MODEL_DEBUG
            resValue "string", "sync_adapter_group", SYNC_ADAPTER_GROUPS_DEBUG
            resValue "string", "sync_adapter_transfer", SYNC_ADAPTER_TRANSFER_DEBUG
            resValue "string", "sync_adapter_integration", SYNC_ADAPTER_INTEGRATION_DEBUG

            buildConfigField "String", "ACCOUNT_ID", "\"" + ACCOUNT_ID_DEBUG + "\""
            buildConfigField "String", "PROVIDER_AUTHORITY", "\"" + PROVIDER_AUTHORITY_DEBUG + "\""

            resValue "string", "hockeyapp_key", ACTIVITI_HOCKEYAPP_KEY

            //ALFRESCO Integration
            buildConfigField "String", "ALFRESCO_APPLICATION_ID", "\"" + ALFRESCO_APPLICATION_ID_DEBUG + "\""
            buildConfigField "String", "ALFRESCO_ACCOUNT_ID", "\"" + ALFRESCO_ACCOUNT_ID_DEBUG + "\""
            buildConfigField "String", "ALFRESCO_PROVIDER_AUTHORITY", "\"" + ALFRESCO_PROVIDER_AUTHORITY_DEBUG + "\""
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
                }
            }
        }
    }

    productFlavors {
        //CUSTOM EXTENSION FLAVOR
        custom {
            //Add here custom configuration
            dependencies {
                //Add here 3rd party dependencies
            }
        }

        //PLAY STORE FLAVOR
        store {
            dependencies {
                //Alfresco Private
                storeCompile "com.activiti.android.app:private-icons:0.1.0@aar"
            }
            repositories {
                maven {
                    url "https://artifacts.alfresco.com/nexus/content/repositories/internal-releases/"
                    credentials {
                        username NEXUS_USERNAME
                        password NEXUS_PASSWORD
                    }
                }
            }
        }
    }
}

dependencies {
    compile project(":activiti-sdk")

    //Support
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'

    //Square
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okio:okio:1.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup:otto:1.3.7'
    compile 'com.squareup.picasso:picasso:2.5.2'

    //3rd Party Lib
    compile 'com.mattprecious.telescope:telescope:1.4.0@aar'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.afollestad:material-dialogs:0.7.5.5'
    compile 'com.makeramen:roundedimageview:2.1.0'
    compile 'me.relex:circleindicator:1.1.4@aar'
    compile "com.daimajia.swipelayout:library:1.2.0@aar"
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:1.0.5'
    compile('com.mikepenz:aboutlibraries:5.0.2@aar') {
        transitive = true
    }

    //Debug
    debugCompile 'net.hockeyapp.android:HockeySDK:3.5.0'
}
